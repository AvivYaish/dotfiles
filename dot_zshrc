# Apps
# Editor: nvim
# Shell: zsh
# Shell styling: oh-my-zsh, oh-my-posh, powerlevel10k
# Terminal multiplexer: byobu
# Dotfile tracking: chezmoi

# Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.zshrc.
# Initialization code that may require console input (password prompts, [y/n]
# confirmations, etc.) must go above this block; everything else may go below.
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

# To customize prompt, run `p10k configure` or edit ~/.p10k.zsh.
[[ ! -f ~/.p10k.zsh ]] || source ~/.p10k.zsh

# I put local programs in .local/bin
local_bin=~/.local/bin
export PATH=${local_bin}:$PATH
if [ ! -d ${local_bin} ]; then
  mkdir -p ${local_bin}
fi

# Install necessary applications
if ! test -f ${local_bin}/antigen.zsh; then
  curl -L git.io/antigen > ${local_bin}/antigen.zsh
fi

source ${local_bin}/antigen.zsh

# Load the oh-my-zsh's library.
antigen use oh-my-zsh

# Bundles from the default repo (robbyrussell's oh-my-zsh).
antigen bundle command-not-found
antigen bundle git
antigen bundle python
antigen bundle pip
antigen bundle npm
antigen bundle fzf
if [[ `uname` == "Darwin" ]]; then
  antigen bundle macos
fi
# Bundles from other repos
antigen bundle marlonrichert/zsh-autocomplete@main
antigen bundle zsh-users/zsh-syntax-highlighting

antigen theme romkatv/powerlevel10k     # Load the theme.
antigen apply   # Tell Antigen that you're done.

if command -v ${local_bin}/homebrew/bin/brew &> /dev/null; then
  export PATH=${local_bin}/homebrew/bin:$PATH
fi
if ! command -v brew &> /dev/null; then
  git clone https://github.com/Homebrew/brew ${local_bin}/homebrew
  eval "$(${local_bin}/homebrew/bin/brew shellenv)"
  brew update --force --quiet
  chmod -R go-w "$(brew --prefix)/share/zsh"
  brew analytics off
fi

# node is required by nvim-coc, a plugin for programming language server protocols (for autocompletions, etc').
# bat is a modern 'cat' replacement, has a nice OneHalfDark syntax highlighting theme.
# lsd is a nicer 'ls'.
# To install useful key bindings and fuzzy completion for FZF: $(brew --prefix)/opt/fzf/install
for app in chezmoi curlie bat lsd git-delta ripgrep universal-ctags tealdeer node byobu fzf; do
  case ${app} in
    git-delta)          appCommand="delta";;  # Snazzier 'diff', with One Half Dark support
    universal-ctags)    appCommand="ctags";;  # If your ctags is outdated, this is required for VIM's tagbar
    ripgrep)            appCommand="rg";;     # Faster, better 'grep'!
    tealdeer)           appCommand="tldr";;   # A good companion for 'man', with succint descriptions and useful examples
    *)                  appCommand=${app};;
  esac
  if ! command -v ${appCommand} &> /dev/null; then
    brew install ${app}
  fi
done
if command -v ${local_bin}/homebrew/bin/byobu &> /dev/null; then
  export BYOBU_PREFIX=${local_bin}/homebrew
fi

if [[ `uname` == "Darwin" ]]; then
  if ! command -v nvim &> /dev/null; then
    brew install neovim
  fi
fi
if [[ `uname` == "Linux" ]]; then
  if ! command -v nvim &> /dev/null; then
    if ! command -v nvim.appimage &> /dev/null; then # Currently assumes you are using Linux on x64
      echo "Installing NeoVIM"
      curl -LO https://github.com/neovim/neovim/releases/latest/download/nvim.appimage
      mv -f nvim.appimage ${local_bin}/
    fi
    if command -v nvim.appimage &> /dev/null; then
      alias nvim="nvim.appimage"
    fi
  fi
fi

alias vim="nvim"
alias vimdiff="nvim -d"
